# Default User
DEFAULT_USER=$USER

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# Deduplicate PATH entries on reload
typeset -U path PATH

# your project folder that we can `c [tab]` to
export PROJECTS=~/Projects

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/post.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
# only regenerate compinit dump once per day (glob qualifier requires EXTENDED_GLOB)
autoload -U compinit
setopt EXTENDED_GLOB
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# load post-compinit files (keybindings, shell integrations, late evals)
for file in ${(M)config_files:#*/post.zsh}
do
  source $file
done

unset config_files
